allprojects {
    apply plugin: 'idea'
    apply plugin: 'maven'
    apply plugin: 'signing'
}

subprojects {
    apply plugin: 'java'
    group = 'com.github.dreamhead'
    version = '0.6-SNAPSHOT'

    repositories {
        mavenCentral()
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: "https://oss.sonatype.org/content/repositories/snapshots") {
                    authentication(userName: sonatypeUsername, password: sonatypePassword)
                }

                pom.project {
                    name project.name
                    packaging 'jar'
                    description 'Moco is Moco is a easy setup stub framework, mainly focusing on testing and integration'
                    url 'https://github.com/dreamhead/moco'

                    scm {
                        url 'scm:git@github.com:dreamhead/moco.git'
                        connection 'scm:git@github.com:dreamhead/moco.git'
                        developerConnection 'git@github.com:dreamhead/moco.git'
                    }

                    licenses {
                        license {
                            name 'The Apache Software License, Version 2.0'
                            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                            distribution 'repo'
                        }
                    }

                    developers {
                        developer {
                            id 'dreamhead'
                            name 'Zheng Ye'
                        }
                    }
                }
            }
        }
    }

    dependencies {
        compile(
                'com.google.guava:guava:13.0.1',
                'io.netty:netty:3.6.1.Final',
                'org.slf4j:slf4j-api:1.7.2',
                'commons-io:commons-io:2.4'
        )

        runtime(
                'com.googlecode.sli4j:sli4j-slf4j-logback:2.0',
                'ch.qos.logback:logback-classic:1.0.9'
        )

        testCompile(
                'junit:junit:4.11',
                'org.mockito:mockito-all:1.9.0',
                'org.apache.httpcomponents:httpcore:4.2.1',
                'org.apache.httpcomponents:httpclient:4.2.1',
                'org.apache.httpcomponents:fluent-hc:4.2.1',
                'commons-logging:commons-logging:1.1.1',
        )
    }

    task sourcesJar(type: Jar) {
        from sourceSets.main.allSource
        classifier = 'sources'
    }

    artifacts {
        archives jar
        archives sourcesJar
    }

    signing {
        sign configurations.archives
    }
}

project(':moco-runner') {
    dependencies {
        compile(
                project(':moco-core'),
                'com.fasterxml.jackson.core:jackson-databind:2.1.0',
                'commons-cli:commons-cli:1.2'
        )

        testCompile project(':moco-core').sourceSets.test.output
        testRuntime project(':moco-core').sourceSets.test.resources
    }

    task uberjar(type: Jar, dependsOn: jar) {
        classifier = 'standalone'

        from files(project(':moco-core').sourceSets.main.output.classesDir)
        from files(project(':moco-core').sourceSets.main.output.resourcesDir)
        from files(sourceSets.main.output.classesDir)
        from configurations.runtime.asFileTree.files.collect { zipTree(it) }

        manifest {
            attributes 'Main-Class': 'com.github.dreamhead.moco.bootstrap.Main'
        }
    }

    artifacts {
        archives uberjar
    }
}

